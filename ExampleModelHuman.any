// Example Model using the Camera template class and the HumanModel
// The camera is configured to look at the pelvis and rotate around 
// the model. 
// 
// See the CameraClassTemplate for detailed usage of the camera class

#include "<ANYBODY_PATH_INSTALLDIR>/AMMR/Application/libdef.any"

#include "CameraClassTemplate.any"


Main = {
  
  VideoLookAtCamera VideoMaker(UP_DIRECTION=y) =
  {       
    // Filename of the video that is created.
    VideoName = "Human";
    
    // Save images at double resolution and downscale when creating 
    // the video. This will improve the video quality.
    VideoInputScale = 2;
    
    VideoResolution = {1080,768};
    
    // The camera direction is time dependent to make a rotating camera. 
    CameraDirection  = {cos(2*pi*Main.Study.t),0,sin(2*pi*Main.Study.t) };
    
    // The point in global reference which the camera looks at. 
    CameraLookAtPoint = Main.HumanModel.BodyModel.Trunk.SegmentsLumbar.PelvisSeg.r+{0,-0.15,0};
    
    // Override the default camera counter with the time step from the study.
    // This ensures that we only save the images from the analysis. 
    // Note: The study must include (have a reference to) this camera template.
    //       Otherwise the counter will not update. 
    Counter = Main.Study.iStep;
    
    // The sequence of analysis to make the recording from.
    Analysis = {
      AnyOperation &ref = Main.Study.Kinematics;
    };
  };
  
  
  
  
  
  // Include the HumanModel from the repository
  #include "<ANYBODY_PATH_BODY>/HumanModel.any"

  AnyBodyStudy Study = {
     AnyFolder &model = .HumanModel.BodyModelWithDefaultDrivers;
     Gravity = {0.0, -9.81, 0.0};

    // We need to include a referece to the camera to ensure the 
    // time dependent variables update when the study runs
    AnyFolder& Camera_referece = Main.VideoMaker;
    
    nStep = 60;

  };  


};// Main


